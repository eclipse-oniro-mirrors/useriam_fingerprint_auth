# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("fingerprintauthservice_ex_config") {
  include_dirs = [ "inc" ]
}

ohos_source_set("fingerprintauthservice_ex_source_set") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  sources = [
    "src/screen_state_monitor.cpp",
    "src/sensor_illumination_task.cpp",
  ]

  include_dirs = [
    "inc",
    "../services/inc",
    "../common/inc",
    "../common/logs",
    "../common/utils",
  ]

  public_configs = [ ":fingerprintauthservice_ex_config" ]

  deps = [ "../services:fingerprintauthservice" ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "display_manager:displaymgr",
    "drivers_interface_fingerprint_auth:libfingerprint_auth_proxy_2.0",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "ipc:ipc_single",
    "power_manager:powermgr_client",
    "samgr:samgr_proxy",
    "user_auth_framework:userauth_executors",
    "window_manager:libdm",
  ]

  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    external_deps += [ "graphic_2d:2d_graphics" ]
    defines = [ "USE_ROSEN_DRAWING" ]
  }

  remove_configs = [ "//build/config/compiler:no_exceptions" ]

  subsystem_name = "useriam"
  part_name = "fingerprint_auth"
}

ohos_shared_library("fingerprintauthservice_ex") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"
  deps = [ ":fingerprintauthservice_ex_source_set" ]

  external_deps = [ "hilog:libhilog" ]

  if (use_musl) {
    version_script = "fingerprint_auth_service_ex_map"
  }

  subsystem_name = "useriam"
  part_name = "fingerprint_auth"
}
